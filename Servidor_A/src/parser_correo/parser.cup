package parser_correo;
import java.io.*;
import java.util.LinkedList;
import java.util.ArrayList;
import java_cup.runtime.*;
import java.util.Iterator;
parser code
{:
public  String lUsuario;
public  String lNombre;
public  String pass;
public int respuesta;
public String msjCuenta;
public String rCorreo="";
    
        public LinkedList<Errores> lista         = new LinkedList<Errores>();
        public LinkedList<usuario> listaUsuarios = new LinkedList<usuario>();
        public usuario usuario;
        public LinkedList<correo> listaCorreos =new LinkedList<correo>();
        public LinkedList<archivo> listaArchivos = new LinkedList<archivo>();
public correo correo;

        public correo_envio correo_envio;

        /*Variable de salida*/
        public String Salida=new String();

            /**Metodo al que se llama automaticamente ante algun error sintactico.*/
public void syntax_error(Symbol s){
       // System.out.println("Error en la Linea " + (s.right+1) +" Columna "+s.left+ ". Identificador " +s.value + " no reconocido." );
        lista.add(new Errores(s.right+1,s.left,"Error sintactico Identificador " +s.value + " no reconocido." ));
}
             /**Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores.*/
public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
       // System.out.println("Error en la Linea " + (s.right+1)+ "Columna "+s.left+". Identificador " + s.value + " no reconocido.");
       // lista.add(new Errores(s.right+1,s.left,"Error sintactico2 Identificador " +s.value + " no reconocido." ));
}

:}
action code
{:
int c=0;
String[] username;
LinkedList<String> destinatarios = new LinkedList<String>();
LinkedList<celdaPG> celdas = new LinkedList<celdaPG>();

:}

terminal String  signos, num, cad, c2, fecha, hora, envio, destinatarios1, destinatarios2, destinatario1, destinatario2;
terminal String inicio, usuarios1, usuarios2, fecha_t, correo_t;
terminal String id, tipo, correo, registro, abrir, cerrar, coma, igual, sesion1, sesion2, usuario1, usuario2, clave1, clave2, nombre1, nombre2, fecha1, fecha2, error1, error2, registro1, registro2;
terminal String peticion, listaC, de, lista, enviado1, enviado2;
terminal String usuario, correos1, correos2, correo1, correo2, asunto1, asunto2, de1, de2, contenido1, contenido2;

terminal String pg, archivos1, archivos2, archivo1, archivo2, tamanio,  fila, columna, color, celda1, celda2; 

non terminal String A1, A2, A3, CPG1, CPG2, A11;

non terminal String INICIA, C1, C2, TEXTO, CADENA;

non terminal usuario  LU1, LU2, LU3;

non terminal String RC1, RC2;

non terminal String IS1, IS2;

non terminal PLC1, PLC2;

non terminal LC1, LC2, LC3;

non terminal RLC1, RLC2, RLC3;

non terminal PC1;

non terminal RPC1;

non terminal RNC1, LD1, LD2;

non terminal MR1;

non terminal REC1, REC2, LREC2, REC3, REC4;

start with INICIA;

INICIA::= C1
        |LU1
        |RC1
        |IS1
        |PLC1
        |LC1
        |RLC1
        |PC1
        |RPC1
        |RNC1
        |MR1
        |REC1;

/********** Crear Cuenta ***************************************/

C1::= abrir sesion1 id igual c2 correo_t c2 tipo igual c2 registro c2 cerrar C2 sesion2;

C2::= usuario1 correo:t1 usuario2  nombre1 TEXTO:t2 nombre2 fecha1 fecha:t31 coma hora:t32 fecha2 clave1 TEXTO:t4 clave2 {:  username = t1.split("@"); parser.usuario = new usuario(t1, username[0], t2, t31+","+t32, t4); :};


/******************Inicia Sesion ****************************/

IS1::= abrir sesion1 id igual c2 correo_t c2 tipo igual c2 inicio c2 cerrar IS2 sesion2;

IS2::= usuario1 correo:t1 usuario2 clave1 TEXTO:t2 clave2  {: username = t1.split("@"); parser.usuario = new usuario(t1, username[0], "", "", t2); :};

/********************peticion lista correos *****************************/

PLC1::= abrir sesion1 id igual c2 correo_t c2 peticion igual c2 listaC c2 cerrar PLC2 sesion2;

PLC2::= usuario1 correo:t1 usuario2          {: parser.lUsuario = t1; parser.respuesta= 3; :};



/**********************Texto archivos ****************************************/
TEXTO::=  CADENA:t1 TEXTO:t2   {:  t1 = t1+" "+t2;  RESULT=t1; :}
        | CADENA:t             {:  RESULT= t.toString();  :};

CADENA::= cad:t     {:  RESULT=t; :}
         |num:t     {:  RESULT=t; :}
         |signos:t  {:  RESULT=t; :} 
        |envio:t  {:  RESULT=t; :} 
         |usuario:t  {:  RESULT=t; :};


/****************** LISTA USUARIOS *********************/

LU1::= usuarios1 LU2 usuarios2;

LU2::= LU3:nt1 LU2:nt2  {:   :}
     | LU3:nt1       {:  :};

LU3::= usuario1 usuario1 correo:t1 usuario2 nombre1 TEXTO:t2 nombre2  fecha1 fecha:t31 coma hora:t32 fecha2 clave1 TEXTO:t4 clave2 usuario2 {: username = t1.split("@");  parser.listaUsuarios.add(new usuario(t1, username[0], t2, t31+","+t32, t4));  :};

/******************Respuesta Mensaje *********************/

RC1::= abrir sesion1 id igual c2 correo_t c2 cerrar RC2 sesion2; 

RC2::= registro1 TEXTO:t1 registro2 {: parser.msjCuenta= t1; parser.respuesta=2; :}
      | error1 TEXTO:t1   error2    {: parser.msjCuenta= t1; parser.respuesta=1; :};


/******************* Lista Correos **************************************/

LC1 ::= abrir correos1 usuario igual c2 correo c2 cerrar LC2 correos2;

LC2 ::= LC3 LC2
        |LC3;

LC3 ::= correo1 asunto1 TEXTO:t1 asunto2 fecha1 fecha:t21 coma hora:t22 fecha2 de1 correo:t3 de2 nombre1 TEXTO:t4 nombre2 contenido1 TEXTO:t5 contenido2 A1 correo2{: c++; parser.listaCorreos.add(new correo(c,"","",t1,t21+","+t22,t3,t4,t5, parser.listaArchivos)); :};


/******************* Respuesta Lista Correos **************************************/

RLC1::= abrir sesion1 id igual c2 correo_t c2 tipo igual c2 lista c2 cerrar RLC2 sesion2  {: parser.respuesta=4; :};

RLC2 ::= RLC3 RLC2
        |RLC3;

RLC3 ::= correo1 usuario1 correo:t1 usuario2 nombre1 TEXTO:t2 nombre2 asunto1 TEXTO:t3 asunto2 fecha1 fecha:t41 coma hora:t42 fecha2 de1 correo:t5 de2 nombre1 TEXTO:t6 nombre2  correo2{: c++; parser.listaCorreos.add(new correo(c,t1,t2,t3,t41+","+t42,t5,t6, "", null));  :};


/******************* PETICION DE UN CORREO **************************************/

PC1::=  abrir sesion1 id igual c2 correo_t c2 peticion igual c2 correo_t c2 de igual c2 correo:t3 c2 fecha_t igual c2 fecha:t21 coma hora:t22 c2 cerrar usuario1 correo:t1 usuario2 sesion2 {: parser.respuesta=5; c++; parser.correo = new correo(c,t1,"","",t21+","+t22,t3,"", "", null);  :};


/*******************Respuesta de un correo ***************************/

RPC1::= correo1 usuario1 correo:t1 usuario2 asunto1 TEXTO:t2 asunto2 fecha1 fecha:t31 coma hora:t32 fecha2 de1 correo:t4 de2 contenido1 TEXTO:t5 contenido2 A1 correo2{: parser.respuesta=6; c++; parser.correo = new correo(c,t1,"",t2,t31+","+t32,t4,"",t5, parser.listaArchivos); :};

/**************************Redactar Correo ***********************************/

RNC1::=  abrir sesion1 peticion igual c2 envio c2 cerrar usuario1 correo:t1 usuario2 destinatarios1 LD1 destinatarios2 asunto1 TEXTO:t2 asunto2 fecha1 fecha:t31 coma hora:t32 fecha2 contenido1 TEXTO:t4 contenido2 A1 sesion2 {: parser.respuesta=7;  parser.correo_envio = new correo_envio(t1,destinatarios,t2,t31+","+t32,t4, parser.listaArchivos); :};

LD1 ::= LD2:t1 LD1:t2  {: destinatarios.add(t1.toString());  :}
        |LD2:t1     {: destinatarios.add(t1.toString()); RESULT=t1; :};

LD2::= destinatario1 correo:t1 destinatario2 {: RESULT= t1; :};


/***************************Archivos ********************************/



A1::= archivos1 A2 archivos2
      | archivos1 archivos2;

A2::= A2 A3 
      |A3;

/***************************Archivos imagenes ****************************/

A3::= archivo1 tipo igual c2 pg:t1 c2 tamanio igual c2 TEXTO:t2 c2 cerrar CPG1 archivo2 {: parser.listaArchivos.add(new archivo(t1, t2, celdas));  :}; 
    
CPG1::= CPG1 CPG2 
      |CPG2;

CPG2::= celda1 fila igual num:t1 columna igual num:t2 color igual c2 TEXTO:t3 c2 cerrar celda2 {: celdas.add(new celdaPG(t1, t2, t3));  :};

/************************* color *******************************/




/*********************Mensaje Recibido ***********************************/

MR1::= correo1 TEXTO:t1 correo2 {: parser.respuesta=8; :};


/*********************Respuesta envio Correos **************************/

REC1::= correo1 LREC2 correo2 {: parser.respuesta=9; :};

LREC2::= REC2 LREC2 
        |REC2;

REC2::= enviado1 REC3 enviado2;

REC3::= destinatario1 correo:t1 destinatario2 REC4:t2 {: parser.rCorreo+=t1+":"+t2+"\n";  :};

REC4::= error1 TEXTO:t1 error2 {: RESULT = t1;  :}
        |{: RESULT= "EXITO"; :};
